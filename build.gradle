plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org"
	}
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
    maven {
        url "https://gitlab.com/api/v4/projects/25805200/packages/maven"
    }
    maven { url 'https://api.modrinth.com/maven' }

    // For Canvas
    maven {
        name = "vram"
        url = "https://maven.vram.io/"
    }
}

dependencies {
	def library = { id ->
        modImplementation(id) { exclude group: "org.slf4j" }
        include id
	}

	// Fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.api_version}"
    shadow(fabricApi.module("fabric-api-base", project.api_version)) {
        transitive = false
    }

    // Compat fixes
    modCompileOnly("me.jellysquid.mods:sodium-fabric:${project.sodium_version}") { transitive = false }
    modCompileOnly("io.vram:canvas-fabric-mc118:1.0.+") { transitive = false }

	// Baritone integration
    library "baritone:baritone:${project.baritone_version}"

    // Libraries
    library "meteordevelopment:orbit:${project.orbit_version}"
    library "meteordevelopment:starscript:${project.starscript_version}"
    library "org.reflections:reflections:${project.reflections_version}"
    include "org.javassist:javassist:3.28.0-GA"
    library "io.gitlab.jfronny.libjf:libjf-unsafe-v0:${project.jfapi_version}"
    include "io.gitlab.jfronny.libjf:libjf-base:${project.jfapi_version}"
    modRuntimeOnly "io.gitlab.jfronny.libjf:libjf-devutil-v0:${project.jfapi_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/meteor-client.accesswidener")
}

shadowJar {
	configurations = [ project.configurations.shadow ]
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding("UTF-8")
}

jar {
	from "LICENSE"
}

remapJar {
	dependsOn shadowJar
	input.set(shadowJar.archiveFile)
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "meteor-client"
            version = project.mod_version

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }
}

apply from: "scripts/packetutils.gradle"
